// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.vKotlin = '1.2.10'
    ext.vSupport = '27.0.1'
    ext.vAnko = '0.10.3'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$vKotlin"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    if (project.name.startsWith("kola")) {

        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'

        // Function that generate versions code from version 'MAJOR.MINOR.PATCH'
        def generateVersionCode = { version ->
            def (major, minor, patch) = version.split('\\.').collect { it as Integer }
            major * 10000 + minor * 100 + patch
        }

        def vName = '0.1.2'

        android {
            compileSdkVersion 27
            buildToolsVersion "27.0.1"

            defaultConfig {
                minSdkVersion 17
                targetSdkVersion 27
                versionCode generateVersionCode(vName)
                versionName vName
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
            }

        }

        dependencies {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$vKotlin"
        }

        def name = project.name

        ext {
            bintrayRepo = 'maven'
            bintrayName = name

            publishedGroupId = 'io.kola'
            libraryName = name
            artifact = name

            libraryDescription = project.description

            siteUrl = 'https://github.com/JimmyRobz/Kola'
            gitUrl = 'https://github.com/JimmyRobz/Kola.git'

            libraryVersion = vName

            developerId = 'jimmyrobz'
            developerName = 'Jimmy Robz'
            developerEmail = 'jimmy@robz.re'

            licenseName = 'MIT License'
            licenseUrl = 'https://opensource.org/licenses/MIT'
            allLicenses = ["MIT"]
        }

        apply from: project.rootProject.file('install.gradle')
        apply from: project.rootProject.file('bintray.gradle')
    }
}